abstract type Named {
    required name: str;
    name_len := len(.name);
    nickname: str;
    nickname_len := len(.nickname);
}

abstract type ExclusivelyNamed {
    required name: str {
        delegated constraint exclusive;
    }
}

type UserGroup extending Named {
    # many-to-many
    multi link users: User;
}

type GameSession {
    required num: int64;
    # one-to-many
    multi link players: User {
        constraint exclusive;
        property is_tall_enough: bool;
    };
}

type User extending Named {
    # test computed backlink
    groups := .<users[is UserGroup];
}

type Post {
    required body: str;
    required link author: User;
}

type AssortedScalars {
    required name: str;
    vals: array<str>;

    date: cal::local_date;
    time: cal::local_time;
    ts: datetime;
    lts: cal::local_datetime;
    bstr: bytes;
}

module sub {
    type TypeInSub {
        post: default::Post;
        subsub: default::sub::subsub::TypeInSubSub;
        ntup: tuple<a: str, b: tuple<c: int64, d: str>>;
    }

    module subsub {
        type TypeInSubSub;
    }
}

module emptysub {
}

# Object with required multi link, no link props
type Party extending ExclusivelyNamed {
    required multi members: User;
}

# Object with required multi link, has link props
type Raid extending ExclusivelyNamed {
    required multi members: User {
        role: str;
        rank: int64;
    };
}

# Object with optional link to self, has link props
type LinearPath {
    required label: str {
        constraint exclusive;
    }
    next: LinearPath {
        constraint exclusive;
        steps: int64 {
            default := 1
        }
    }
}
